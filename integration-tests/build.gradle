plugins {
    id 'eu.xenit.docker-alfresco' version '5.3.1' apply false
    id 'eu.xenit.docker-compose' version '5.2.0' apply false    
}

subprojects {
    def edition = project.name.split("-")[1]
    apply from: "${project.projectDir}/overload.gradle"

    description = "Alfresco ${alfrescoVersion} with solr query analytics"

    apply plugin: 'eu.xenit.docker-alfresco'
    apply plugin: 'eu.xenit.docker-compose'    

    dependencies {
        alfrescoAmp project(path: ":alfresco-solr-query-analytics-6x", configuration: "amp")

        //starting from the 5.3.1 plugin, the platform command below will find your matching base-war
        baseAlfrescoWar platform("org.alfresco:acs-packaging:${alfrescoVersion}")
        baseAlfrescoWar 'org.alfresco:content-services@war'

        alfrescoAmp "${apixImplAmpVersion}"
        alfrescoAmp "${dynamicExtensionsAmpVersion}"
        alfrescoAmp "eu.xenit.alfresco:alfresco-hotfix-MNT-20557:1.0.2@amp"
        alfrescoAmp "${care4alfAmpVersion}"

    }

    dockerBuild	{
      alfresco {    
        def namespace = (edition=="community"?"public":"alfresco-${edition}")
        baseImage = "hub.xenit.eu/${namespace}/alfresco-${edition}:${alfrescoVersion}"
        leanImage = true
      }
    }


    dockerCompose {
        dockerComposeWorkingDirectory = "${project.parent.projectDir}/src/main/compose/"

        useComposeFiles = ["docker-compose.yml",
                           "docker-compose-sharded-solr.yml"]

        nonSharded {
            useComposeFiles = ["docker-compose.yml",
                               "docker-compose-solr.yml"]
        }
    }

}
