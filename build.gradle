subprojects {
    description = "Alfresco solr query analytics"

    group = 'eu.xenit.alfresco'
    version = version = '2.0.1' + getVersionQualifier()
    ext.isReleaseVersion = !(version.endsWith("SNAPSHOT"))

    // It is not possible to set properties with a dot via GitHub Actions env variables, therefore we introduce support
    // for a non-dotted-equivalent
    copyPropertyValueIfExists('alfresco_nexus_username', 'org.alfresco.maven.nexus.username')
    copyPropertyValueIfExists('alfresco_nexus_password', 'org.alfresco.maven.nexus.password')
    copyPropertyValueIfExists('cloudsmith_username', 'eu.xenit.cloudsmith.username')
    copyPropertyValueIfExists('cloudsmith_password', 'eu.xenit.cloudsmith.password')


    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
        }
	    maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/private'
            credentials {
                username project.property('org.alfresco.maven.nexus.username')
                password project.property('org.alfresco.maven.nexus.password')
            }
        }
	    // for alfresco's dependency org.vaadin.addons:dcharts-widget
	    maven {
	        url 'https://maven.vaadin.com/vaadin-addons'
	    }
    }
}

def String getVersionQualifier() {
    def branch_name = System.getenv("BRANCH_NAME")?:"local"

    if (project.hasProperty('qualifier')) {
        return '-' + project.getProperty('qualifier')
    } else if (branch_name == 'release') {
        return ''
    } else {
        return '-SNAPSHOT'
    }
}

def copyPropertyValueIfExists(sourcePropertyName, targetPropertyName) {
    if (project.hasProperty(sourcePropertyName)) {
        project.ext[targetPropertyName] = project.property(sourcePropertyName)
    }
}
