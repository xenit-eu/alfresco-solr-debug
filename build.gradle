plugins {
    id 'io.codearte.nexus-staging' version '0.21.0'
}


nexusStaging {
    packageGroup = 'eu.xenit'
    username = System.env.PUBLISH_USER != null ? System.env.PUBLISH_USER : ''
    password = System.env.PUBLISH_PASSWORD != null ? System.env.PUBLISH_PASSWORD : ''
}


subprojects {
    description = "Alfresco solr debugging"

    group = 'eu.xenit.alfresco'
    version = '0.0.4' + getVersionQualifier(System.env.BRANCH_NAME ?: 'local')

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
        }
        maven {
            url 'https://artifactory.xenit.eu/artifactory/libs-release'
            credentials {
                username property("eu.xenit.artifactory.username")
                password property("eu.xenit.artifactory.password")
            }
        }
    }
}

def String getVersionQualifier(String branch_name) {
    if (project.hasProperty('qualifier')) {
        return '-' + project.getProperty('qualifier')
    } else if (branch_name == 'release') {
        return ''
    } else {
        return '-SNAPSHOT'
    }
}

