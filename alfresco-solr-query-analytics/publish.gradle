apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            artifact tasks.amp
        }
        all {
            pom {
                url = 'https:/github.com/xenit-eu/alfresco-solr-query-analytics'
                name = "alfresco-solr-query-analytics-74"
                description = "Creates an amp + jar for having Alfresco log more information from solr"
                artifactId = "alfresco-solr-query-analytics-74"

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/alfresco-solr-query-analytics.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/alfresco-solr-query-analytics.git'
                    url = 'https://github.com/xenit-eu/alfresco-solr-query-analytics.git'
                }
                developers {
                    developer {
                        id = "xenit"
                        name = "Xenit Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.toString().endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty('sonatype_username')
                password = project.findProperty('sonatype_password')
            }
        }
    }
}

signing {
    required { !version.toString().endsWith("SNAPSHOT") }
    useInMemoryPgpKeys(findProperty("signingKeyId"), findProperty("signingKey"), findProperty("signingPassword"))
    sign publishing.publications
}